apply plugin: 'jacoco'
apply plugin: 'java'

sourceSets.test.resources.srcDirs = ["src/test/resources"]

dependencies {
    testImplementation project(":core")
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testImplementation arcModule("backends:backend-headless")
    testImplementation "org.json:json:20230618"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    
    reports {
        xml {
            required = true
            destination = file("${project.rootDir}/jacocoTestReport.xml")
        }
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "**/android/**",
                "**/ios/**",
                "**/tools/**"
            ])
        }))
    }

    // Include core project sources
    dependsOn(project(':core').tasks.classes)
    sourceSets project(':core').sourceSets.main
    additionalSourceDirs.from = files(project(':core').sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(project(':core').sourceSets.main.output)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
    }
}

test {
    useJUnitPlatform()
    workingDir = file("../core/assets")
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    finalizedBy jacocoTestReport
}
